buildscript {
    repositories {
        mavenCentral()
        jcenter()
        // maven {
        //    url uri('https://oss.sonatype.org/content/repositories/snapshots/')
        // }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.1'
        // classpath 'com.github.ksoichiro:gradle-eclipse-aar-plugin:0.1.3'
    }
}

apply plugin: 'com.android.application'
// apply plugin: 'com.github.ksoichiro.eclipse.aar'

group = 'com.dsatab'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
	maven { url uri('https://mint.splunk.com/gradle/') }
    maven { url uri("https://jitpack.io") }
}

dependencies {
    compile 'com.google.android.gms:play-services-identity:7.8.0'
    compile 'com.google.android.gms:play-services-drive:7.8.0'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:gridlayout-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:multidex:1.0.1'

    compile 'com.github.clans:fab:1.6.0'
    compile 'net.steamcrafted:materialiconlib:1.0.4'

    compile 'com.github.chrisbanes.photoview:library:1.2.4'
    compile 'de.hdodenhof:circleimageview:1.3.0'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.4'
    compile 'org.osmdroid:osmdroid-android:4.3'

    compile 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.8.1'

    // Driven
    compile 'com.bingzer.android.driven:driven:1.0.0'
    compile 'com.bingzer.android.driven:driven-gdrive:1.0.0'
    compile 'com.bingzer.android.driven:driven-dropbox:1.0.0'
    compile 'com.bingzer.android.driven:driven-ext-drive:1.0.0'
    
    // ORM Lite
    compile 'com.j256.ormlite:ormlite-core:4.48'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'org.slf4j:slf4j-android:1.7.10'

    compile 'org.jdom:jdom2:2.0.5'

    compile "com.splunk.mint:mint:4.4.0"

    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.opencsv:opencsv:3.5'

    compile('com.mikepenz:aboutlibraries:5.2.3@aar') {
        transitive = true
    }

    compile 'com.github.tajchert:nammu:1.1.1'

    compile 'com.github.paolorotolo:appintro:3.2.0'

    compile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    //debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    //releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'

    compile project(':guiLib')
    // compile fileTree(dir: 'libs')
}


// Configure plugin
//eclipseAar {
//    androidTarget = 'android-23'
//    aarDependenciesDir = 'aarDependencies'
//}


android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        // Enabling multidex support.
        multiDexEnabled true

        minSdkVersion 15
        targetSdkVersion 22

        versionName "4.4.5"
        versionCode 106
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']            
            jniLibs.srcDirs = ['libs'] 
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }

    signingConfigs {
        release {
            storeFile file("../../keystore")            
            storePassword System.getenv("ANDROID_KEYSTORE_PWD")
            keyAlias System.getenv("ANDROID_KEYSTORE_ALIAS")
            keyPassword System.getenv("ANDROID_KEYSTORE_PWD")
        }
    }

    productFlavors {
        dev {
            // dev utilizes minSDKVersion = 21 to allow the Android gradle plugin
            // to pre-dex each module and produce an APK that can be tested on
            // Android Lollipop without time consuming dex merging processes.
            minSdkVersion 21
            multiDexEnabled true
        }

        prod {
        }
    }
    buildTypes {
        debug {
            versionNameSuffix " Debug"
        }
        release {
            shrinkResources true
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

tasks.withType(JavaCompile) { options.encoding = "UTF-8" }