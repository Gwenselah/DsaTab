buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0'
    }
}

apply plugin: 'com.android.application'

group = 'com.dsatab'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url uri('https://mint.splunk.com/gradle/') }
    maven { url uri("https://jitpack.io") }
}

// an attempt to fix "Warning:Conflict with dependency 'com.google.code.findbugs:jsr305'. Resolved versions for app (1.3.9) and test app (2.0.1) differ."
// http://stackoverflow.com/questions/19008006/accessing-configurations-resolutionstrategy-from-a-plugin
// this block triggers a "method call is ambiguous error" which seems to be harmless:
// http://stackoverflow.com/questions/25177361/gradle-configurations-all-ambiguous
configurations.all {
    resolutionStrategy.force 'com.google.code.findbugs:jsr305:2.0.1'
}
dependencies {
    compile 'com.android.support:cardview-v7:24.2.1'
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:recyclerview-v7:24.2.1'
    compile 'com.android.support:gridlayout-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.android.support:multidex:1.0.1'

    compile 'com.github.clans:fab:1.6.0'
    compile 'net.steamcrafted:materialiconlib:1.0.4'

    compile 'com.github.chrisbanes.photoview:library:1.2.4'
    compile 'de.hdodenhof:circleimageview:1.3.0'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    compile 'org.osmdroid:osmdroid-android:5.4.1:release@aar'

    compile 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.8.1'

    // CloudRail
    compile 'com.cloudrail:cloudrail-si-android:2.7.2'

    // ORM Lite
    compile 'com.j256.ormlite:ormlite-core:5.0'
    compile 'com.j256.ormlite:ormlite-android:5.0'
    compile 'org.slf4j:slf4j-android:1.7.10'

    compile 'org.jdom:jdom2:2.0.6'

    compile "com.splunk.mint:mint:5.0.0"

    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.opencsv:opencsv:3.5'

    compile('com.mikepenz:aboutlibraries:5.8.1@aar') {
        transitive = true
    }

    compile 'com.github.tajchert:nammu:1.1.1'

    compile 'com.github.paolorotolo:appintro:4.1.0'

    compile project(':guiLib')
    // compile fileTree(dir: 'libs')

    // needed to read xml dom in testcase
    androidTestCompile('com.android.support.test:runner:0.4.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:rules:0.4.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.0') {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    defaultConfig {
        // Enabling multidex support.
        multiDexEnabled true

        minSdkVersion 15
        targetSdkVersion 24

        versionName "4.5"
        versionCode 110

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        //generatedDensities = []
        vectorDrawables.useSupportLibrary = true
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }

    signingConfigs {
        release {
            storeFile file("../../keystore")
            storePassword System.getenv("ANDROID_KEYSTORE_PWD")
            keyAlias System.getenv("ANDROID_KEYSTORE_ALIAS")
            keyPassword System.getenv("ANDROID_KEYSTORE_PWD")
        }
    }

    buildTypes {
        debug {
            versionNameSuffix " Debug"
        }
        release {
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

tasks.withType(JavaCompile) { options.encoding = "UTF-8" }