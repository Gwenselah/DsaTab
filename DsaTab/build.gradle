buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

configurations {
    all {
        exclude module: 'httpclient'
        exclude module: 'commons-logging'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

group = 'com.dsatab'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    google()
    maven { url 'https://maven.fabric.io/public' }
    maven { url uri("https://jitpack.io") }
}

dependencies {
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:gridlayout-v7:28.0.0'
    implementation 'com.android.support:preference-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'

    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.github.fafaldo:fab-toolbar:1.2.0'
    implementation 'net.steamcrafted:materialiconlib:1.0.4'
    implementation 'com.github.chrisbanes:PhotoView:2.0.0'
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    implementation 'org.osmdroid:osmdroid-android:5.6.5'
    implementation 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.10.2.1'
    // CloudRail
    implementation 'com.cloudrail:cloudrail-si-android:2.22.4'
    // ORM Lite
    implementation 'com.j256.ormlite:ormlite-core:5.0'
    implementation 'com.j256.ormlite:ormlite-android:5.0'
    implementation 'org.slf4j:slf4j-android:1.7.25'
    implementation 'org.jdom:jdom2:2.0.6'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true
    }
    implementation 'com.squareup.okhttp:okhttp:2.7.5'
    implementation 'com.opencsv:opencsv:4.0'
    implementation('com.mikepenz:aboutlibraries:6.0.0@aar') {
        transitive = true
    }
    implementation ('com.github.tajchert:nammu:1.2.1') {
        exclude group: 'com.android.support', module: 'support-v4'
    }
    implementation 'com.github.paolorotolo:appintro:4.1.0'
    // needed to read xml dom in testcase
    androidTestImplementation('com.android.support.test:runner:0.6-alpha') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation('com.android.support.test:rules:0.6-alpha') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.3-alpha') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:2.3-alpha') {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        // Enabling multidex support.
        multiDexEnabled true

        minSdkVersion 15
        targetSdkVersion 28

        versionName "4.7.1"
        versionCode 128

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "google_drive_secret", GoogleDriveSecret

        buildConfigField "String", "dropbox_client_id", DropboxClientId
        buildConfigField "String", "dropbox_secret", DropboxSecret

        buildConfigField "String", "onedrive_client_id", OneDriveClientId
        buildConfigField "String", "onedrive_secret", OneDriveSecret

        // buildConfigField "String", "bugsense_api_key", BUGSENSE_API_KEY

        buildConfigField "String", "cloudrail_api_key", CLOUDRAIL_API_KEY

        manifestPlaceholders = [fabric_api_key: FABRIC_API_KEY]

    }



    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }

    signingConfigs {
        release {
            storeFile file("../../keystore")
            storePassword ANDROID_STORE_PWD
            keyAlias ANDROID_KEYSTORE_ALIAS
            keyPassword ANDROID_KEY_PWD
        }
    }

    buildTypes {
        debug {
            versionNameSuffix " Debug"

            buildConfigField "String", "google_drive_client_id", GoogleDriveClientIdDebug

            signingConfig signingConfigs.release
        }
        release {
            buildConfigField "String", "google_drive_client_id", GoogleDriveClientIdRelease

            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

tasks.withType(JavaCompile) { options.encoding = "UTF-8" }